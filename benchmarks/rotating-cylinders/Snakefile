# benchmarks/rotating-cylinders/Snakefile
import json
import os

if not os.path.exists("rotating-cylinders_config.json"):
    os.system("python3 generate_rc_config.py")

configfile: "rotating-cylinders_config.json"

# Variables
tools = config["tools"]
configs = config["configurations"]
benchmark = config["benchmark"]
benchmark_uri = config.get("benchmark_uri", "https://nfdi-benchmark.org/rc") 
result_dir = f"snakemake_results/{benchmark}"
shared_dir = os.getcwd()
dumux_dir = f"{shared_dir}/dumux"

rule all:
    input:
        expand(f"{result_dir}/{{tool}}/summary.json", tool=tools)

rule generate_dumux_inputs:
    input:
        grid_t = f"{dumux_dir}/grid_files/grid_template.json",
        dumux_t = f"{dumux_dir}/input_files/dumux_config.json"
    output:
        params = expand(f"{dumux_dir}/input_files/params_{{conf}}.input", conf=configs),
        grids = expand(f"{dumux_dir}/grid_files/grid_{{conf}}.json", conf=configs)
    shell:
        "python3 dumux_input_gen.py --grid_template {input.grid_t} --dumux_template {input.dumux_t}"

for tool in tools:
    include: f"{tool}/Snakefile"

rule summary:
    input:
        script = "../common/summarize_results.py",
        # We use the grids generated by the generator rule
        parameters = expand(f"{dumux_dir}/grid_files/grid_{{conf}}.json", conf=configs),
        mesh = expand(f"{dumux_dir}/grid_files/grid_{{conf}}.json", conf=configs), 
        metrics = lambda wildcards: expand(
            f"{result_dir}/{{tool}}/solution_metrics_{{conf}}.json",
            tool=[wildcards.tool], conf=configs
        ),
        solution_field_data = lambda wildcards: expand(
            f"{result_dir}/{{tool}}/solution_field_data_{{conf}}.zip",
            tool=[wildcards.tool], conf=configs
        )
    output:
        summary_json = f"{result_dir}/{{tool}}/summary.json"
    shell:
        """
        python3 {input.script} \
            --input_configuration {configs} \
            --input_parameter_file {input.parameters} \
            --input_mesh_file {input.mesh} \
            --input_solution_metrics {input.metrics} \
            --input_solution_field_data {input.solution_field_data} \
            --input_benchmark {benchmark} \
            --input_benchmark_uri {benchmark_uri} \
            --output_summary_json {output.summary_json}
        """